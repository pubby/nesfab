/*
 * Copyright (c) 2022, Patrick Bene
 * This file is distributed under the Boost Software License, Version 1.0.
 * See LICENSE_1_0.txt or https://www.boost.org/LICENSE_1_0.txt 
 */

// Code for decompressing the PBZ format.
// PBZ is a simple run-length encoding based on PB53 by Damian Yerrick.
// The decompressed size must be a multiple of 16 bytes, meaning it can't work with arbitrary data.
// It is most useful for storing CHR data.

// PBZ format:
// -----------
// A sequence of chunks that decompress to 8 bytes each.
// The first byte of a chunk encodes its run-length encoding in a unary-encoded format.
// For each bit of this byte, starting from the high bit:
// -  A '0' bit means read a byte from the stream and output it.
// -  A '1' bit means output the previous byte outputted for this chunk, or $00 if none was.

// This is used to upload data in the PBZ format to the PPU.
// There are two ways to call this function:
// - ppu_upload_pbz.tiles:
//       Reads from 'ptr' and uploads to PPUDATA for ('amount' * 16) bytes of data.
//       Returns a pointer to one past the end of the data.
//       An 'amount' value of 0 is treated as a value of 256.
// - ppu_upload_pbz.chunks:
//       Reads from 'ptr' and uploads to PPUDATA for ('amount' * 8) bytes of data.
//       Returns a pointer to one past the end of the data.
//       An 'amount' value of 0 is treated as a value of 256.
asm fn ppu_upload_pbz(CCC/pbz ptr, U amount) CCC/pbz
: employs /pbz
: +static_fixed
    vars
        U run_mask
    label write_plane
        ldy #0
        lda (&ptr.a), y
        sec
        rol
        sta &run_mask
        tya
    label pb8_loop
        bcs no_new_byte
        iny
        lda (&ptr.a), y
    label no_new_byte
        sta PPUDATA
        asl &run_mask
        bne pb8_loop
        tya
        adc &ptr.a
        sta &ptr.a
        bcc done_inc
        inc &ptr.b
    label done_inc
        rts
    default
    label tiles
        if __fixed
            push
        if __illegal
            lax &ptr.bank
            switch ax
        else
            lda &ptr.bank
            switch a
    label tiles_loop
        jsr write_plane
        jsr write_plane
        dec &amount
        bne tiles_loop
        beq do_return
    label chunks
        if __fixed
            push
        if __illegal
            lax &ptr.bank
            switch ax
        else
            lda &ptr.bank
            switch a
    label chunks_loop
        jsr write_plane
        dec &amount
        bne chunks_loop
    label do_return
        lda &ptr.a
        sta &return.a
        lda &ptr.b
        sta &return.b
        lda &ptr.c
        sta &return.c
        if __fixed
            pop
        rts

