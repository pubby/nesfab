/*
 * Copyright (c) 2024, Patrick Bene
 * This file is distributed under the Boost Software License, Version 1.0.
 * See LICENSE_1_0.txt or https://www.boost.org/LICENSE_1_0.txt 
 */

// This file is based on the work of Antoine Gohin (Broke Studio),
// with original copyright to him.
// See: https://github.com/BrokeStudio/rainbow-net/blob/master/rainbow-net.md

// This file depends on:
// - 'mapper/rainbow.fab'
// - 'charmap/ascii.fab'

///////////////////////////////////////////////////////////////////////////////
// Registers //////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

ct AA NET_CONFIG  = $4190
ct AA NET_RX      = $4191
ct AA NET_TX      = $4192
ct AA NET_RX_ADDR = $4193
ct AA NET_TX_ADDR = $4194

///////////////////////////////////////////////////////////////////////////////
// Buffers ////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// These two buffers handle in/out messages (RX and TX).
ct AA NET_IN  = $4800
ct AA NET_OUT = NET_IN + 256

ct MM net_in  = MM(NET_IN)
ct MM net_out = MM(NET_OUT)

///////////////////////////////////////////////////////////////////////////////
// Outgoing commands to the ESP ///////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// ESP Commands
ct U O_ESP_GET_STATUS = 0                    // Get ESP status
ct U O_DEBUG_GET_LEVEL = 1                   // Get debug level
ct U O_DEBUG_SET_LEVEL = 2                   // Set debug level
ct U O_DEBUG_LOG = 3                         // Debug / Log data
ct U O_BUFFER_CLEAR_RX_TX = 4                // Clear RX/TX buffers
ct U O_BUFFER_DROP_FROM_ESP = 5              // Drop messages from ESP buffer (TX)
ct U O_ESP_GET_FIRMWARE_VERSION = 6          // Get ESP/Rainbow firmware version
ct U O_ESP_FACTORY_RESET = 7                 // Reset ESP to factory settings
ct U O_ESP_RESTART = 8                       // Restart ESP

// WIFI Commands
ct U O_WIFI_GET_STATUS = 9                   // Get Wi-Fi connection status
ct U O_WIFI_GET_SSID = 10                    // Get Wi-Fi network SSID
ct U O_WIFI_GET_IP_ADDRESS = 11              // Get Wi-Fi IP address
ct U O_WIFI_GET_MAC_ADDRESS = 59             // Get Wi-Fi mac address
ct U O_WIFI_GET_CONFIG = 12                  // Get Wi-Fi station / Access Point / Web Server config
ct U O_WIFI_SET_CONFIG = 13                  // Set Wi-Fi station / Access Point / Web Server config

// ACESS POINT Commands
ct U O_AP_GET_SSID = 14                      // Get Access Point network SSID
ct U O_AP_GET_IP_ADDRESS = 15                // Get Access Point IP address

// RND Commands
ct U O_RND_GET_BYTE = 16                     // Get random byte
ct U O_RND_GET_BYTE_RANGE = 17               // Get random byte between custom min/max
ct U O_RND_GET_WORD = 18                     // Get random word
ct U O_RND_GET_WORD_RANGE = 19               // Get random word between custom min/max

// SERVER Commands
ct U O_SERVER_GET_STATUS = 20                // Get server connection status
ct U O_SERVER_PING = 21                      // Get ping between ESP and server
ct U O_SERVER_SET_PROTOCOL = 22              // Set protocol to be used to communicate (WS/UDP/TCP)
ct U O_SERVER_GET_SETTINGS = 23              // Get current server host name and port
ct U O_SERVER_SET_SETTINGS = 24              // Set current server host name and port
ct U O_SERVER_GET_SAVED_SETTINGS = 25        // Get server host name and port values saved in the Rainbow config file
ct U O_SERVER_SET_SAVED_SETTINGS = 26        // Set server host name and port values saved in the Rainbow config file
ct U O_SERVER_RESTORE_SETTINGS = 27          // Restore server host name and port to values defined in the Rainbow config
ct U O_SERVER_CONNECT = 28                   // Connect to server
ct U O_SERVER_DISCONNECT = 29                // Disconnect from server
ct U O_SERVER_SEND_MESSAGE = 30              // Send message to server

// UDP ADDRESS POOL Commands
ct U O_UDP_ADDR_POOL_CLEAR = 55              // Clear the UDP address pool
ct U O_UDP_ADDR_POOL_ADD = 56                // Add an IP address to the UDP address pool
ct U O_UDP_ADDR_POOL_REMOVE = 57             // Remove an IP address from the UDP address pool
ct U O_UDP_ADDR_POOL_SEND_MSG = 58           // Send a message to all the addresses in the UDP address pool

// NETWORK Commands
ct U O_NETWORK_SCAN = 31                     // Scan networks around synchronously or asynchronously
ct U O_NETWORK_GET_SCAN_RESULT = 32          // Get result of the last scan
ct U O_NETWORK_GET_SCANNED_DETAILS = 33      // Get scanned network details
ct U O_NETWORK_GET_REGISTERED = 34           // Get registered networks status
ct U O_NETWORK_GET_REGISTERED_DETAILS = 35   // Get registered network SSID
ct U O_NETWORK_REGISTER = 36                 // Register network
ct U O_NETWORK_UNREGISTER = 37               // Unregister network
ct U O_NETWORK_SET_ACTIVE = 38               // Set active network

// FILE COMMANDS
ct U O_FILE_OPEN = 39                        // Open working file
ct U O_FILE_CLOSE = 40                       // Close working file
ct U O_FILE_STATUS = 41                      // Get working file status
ct U O_FILE_EXISTS = 42                      // Check if file exists
ct U O_FILE_DELETE = 43                      // Delete a file
ct U O_FILE_SET_CUR = 44                     // Set working file cursor position a file
ct U O_FILE_READ = 45                        // Read working file (at specific position)
ct U O_FILE_WRITE = 46                       // Write working file (at specific position)
ct U O_FILE_APPEND = 47                      // Append data to working file
ct U O_FILE_COUNT = 48                       // Count files in a specific path
ct U O_FILE_GET_LIST = 49                    // Get list of existing files in a path
ct U O_FILE_GET_FREE_ID = 50                 // Get an unexisting file ID in a specific path
ct U O_FILE_GET_FS_INFO = 51                 // Get file system details (ESP flash or SD card)
ct U O_FILE_GET_INFO = 52                    // Get file info (size + crc32)
ct U O_FILE_DOWNLOAD = 53                    // Download a file
ct U O_FILE_FORMAT = 54                      // Format file system and save current config

///////////////////////////////////////////////////////////////////////////////
// Incoming commands from the ESP /////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// ESP Commands
ct U I_READY = 0                         // ESP is ready
ct U I_DEBUG_LEVEL = 1                   // Return debug configuration
ct U I_ESP_FIRMWARE_VERSION = 2          // Return ESP/Rainbow firmware version
ct U I_ESP_FACTORY_RESET = 3             // See ESP_FACTORY_RESET_RES enum for details on returned value

// WIFI / ACCESS POINT Commands
ct U I_WIFI_STATUS = 4                   // Return Wi-Fi connection status
ct U I_SSID = 5                          // Return Wi-Fi / Access Point SSID
ct U I_IP_ADDRESS = 6                    // Return Wi-Fi / Access Point IP address
ct U I_MAC_ADDRESS = 28                  // Return Wi-Fi mac address
ct U I_WIFI_CONFIG = 7                   // Return Wi-Fi station / Access Point / Web Server status

// RND Commands
ct U I_RND_BYTE = 8                      // Return random byte value
ct U I_RND_WORD = 9                      // Return random word value

// SERVER Commands
ct U I_SERVER_STATUS = 10                // Return server connection status
ct U I_SERVER_PING = 11                  // Return min, max and average round-trip time and number of lost packets
ct U I_SERVER_SETTINGS = 12              // Return server settings (host name + port)
ct U I_MESSAGE_FROM_SERVER = 13          // Message from server

// NETWORK Commands
ct U I_NETWORK_SCAN_RESULT = 14          // Return result of last scan
ct U I_NETWORK_SCANNED_DETAILS = 15      // Return details for a scanned network
ct U I_NETWORK_REGISTERED_DETAILS = 16   // Return SSID for a registered network
ct U I_NETWORK_REGISTERED = 17           // Return registered networks status

// FILE Commands
ct U I_FILE_STATUS = 18                  // Return working file status
ct U I_FILE_EXISTS = 19                  // Return if file exists or not
ct U I_FILE_DELETE = 20                  // See FILE_DELETE_RES enum for details on returned value
ct U I_FILE_LIST = 21                    // Return path file list
ct U I_FILE_DATA = 22                    // Return file data
ct U I_FILE_COUNT = 23                   // Return file count in a specific path
ct U I_FILE_ID = 24                      // Return a free file ID
ct U I_FILE_FS_INFO = 25                 // Return file system info
ct U I_FILE_INFO = 26                    // Return file info (size + CRC32)
ct U I_FILE_DOWNLOAD = 27                // See FILE_DOWNLOAD_RES enum for details on returned value

///////////////////////////////////////////////////////////////////////////////
// Other enumerations /////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// ESP factory reset result codes
ct U ESP_RESET_SUCCESS = 0
ct U ESP_RESET_ERROR_WHILE_SAVING_CONFIG = 1
ct U ESP_RESET_ERROR_WHILE_DELETING_TWEB = 2
ct U ESP_RESET_ERROR_WHILE_DELETING_WEB = 3

// Wi-Fi status
ct U WIFI_STATUS_TIMEOUT = 255
ct U WIFI_STATUS_IDLE_STATUS = 0
ct U WIFI_STATUS_NO_SSID_AVAIL = 1
ct U WIFI_STATUS_SCAN_COMPLETED = 2
ct U WIFI_STATUS_CONNECTED = 3
ct U WIFI_STATUS_CONNECTION_FAILED = 4
ct U WIFI_STATUS_CONNECTION_LOST = 5
ct U WIFI_STATUS_WRONG_PASSWORD = 6
ct U WIFI_STATUS_DISCONNECTED = 7

// Wi-Fi error
ct U WIFI_ERROR_UNKNOWN = 255
ct U WIFI_ERROR_NO_ERROR = 0
ct U WIFI_ERROR_NO_SSID_AVAIL = 1
ct U WIFI_ERROR_CONNECTION_FAILED = 4
ct U WIFI_ERROR_CONNECTION_LOST = 5
ct U WIFI_ERROR_WRONG_PASSWORD = 6

// Server protocols
ct U SERVER_PROTOCOL_TCP = 0
ct U SERVER_PROTOCOL_TCP_SECURED = 1
ct U SERVER_PROTOCOL_UDP = 2
ct U SERVER_PROTOCOL_UDP_POOL = 3

// Server status
ct U SERVER_STATUS_DISCONNECTED = 0
ct U SERVER_STATUS_CONNECTED = 1

// Wi-Fi config flags
ct U WIFI_FLAGS_WIFI_STATION_ENABLE = %001
ct U WIFI_FLAGS_ACCESS_POINT_ENABLE = %010
ct U WIFI_FLAGS_WEB_SERVER_ENABLE   = %100

// File paths
ct U FILE_PATHS_SAVE = 0
ct U FILE_PATHS_ROMS = 1
ct U FILE_PATHS_USER = 2

// File constants
ct U NUM_PATHS = 3
ct U NUM_FILES = 64

// Network encryption types
ct U NETWORK_ENCRYPT_WEP = 5
ct U NETWORK_ENCRYPT_WPA_PSK = 2
ct U NETWORK_ENCRYPT_WPA2_PSK = 4
ct U NETWORK_ENCRYPT_OPEN_NETWORK = 7
ct U NETWORK_ENCRYPT_WPA_WPA2_PSK = 8

// File config masks/flags
ct U FILE_FLAGS_ACCESS_MODE_MASK = %00000001
ct U FILE_FLAGS_ACCESS_MODE_AUTO = 0
ct U FILE_FLAGS_ACCESS_MODE_MANUAL = 1
ct U FILE_FLAGS_DESTINATION_MASK = %00000010
ct U FILE_FLAGS_DESTINATION_ESP = 0
ct U FILE_FLAGS_DESTINATION_SD = 2

// FILE_DELETE result codes
ct U FILE_DELETE_SUCCESS = 0
ct U FILE_DELETE_ERROR_WHILE_DELETING = 1
ct U FILE_DELETE_FILE_NOT_FOUND = 2
ct U FILE_DELETE_INVALID_PATH_OR_FILE = 3

// FILE_DOWNLOAD result codes
ct U FILE_DOWNLOAD_SUCCESS = 0
ct U FILE_DOWNLOAD_INVALID_DESTINATION = 1
ct U FILE_DOWNLOAD_ERROR_WHILE_DELETING_FILE = 2
ct U FILE_DOWNLOAD_UNKNOWN_OR_UNSUPPORTED_PROTOCOL = 3
ct U FILE_DOWNLOAD_NETWORK_ERROR = 4
ct U FILE_DOWNLOAD_HTTP_STATUS_NOT_IN_2XX = 5

// FILE_DOWNLOAD network error codes
ct U FILE_DOWNLOAD_CONNECTION_FAILED = U(-1)
ct U FILE_DOWNLOAD_SEND_HEADER_FAILED = U(-2)
ct U FILE_DOWNLOAD_SEND_PAYLOAD_FAILED = U(-3)
ct U FILE_DOWNLOAD_NOT_CONNECTED = U(-4)
ct U FILE_DOWNLOAD_CONNECTION_LOST = U(-5)
ct U FILE_DOWNLOAD_NO_STREAM = U(-6)
ct U FILE_DOWNLOAD_NO_HTTP_SERVER = U(-7)
ct U FILE_DOWNLOAD_TOO_LESS_RAM = U(-8)
ct U FILE_DOWNLOAD_ENCODING = U(-9)
ct U FILE_DOWNLOAD_STREAM_WRITE = U(-10)
ct U FILE_DOWNLOAD_READ_TIMEOUT = U(-11)

///////////////////////////////////////////////////////////////////////////////
// Init Functions /////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// Call to setup the ESP / WiFi buffer addresses.
// Returns 'true' if the initialization succeeded, 'false' otherwise.
// On failure, you might try calling 'net_init' a few more times.
fn net_init() Bool
: -inline
    {NET_RX_ADDR}(NET_IN.b)
    {NET_TX_ADDR}(NET_OUT.b)
    {NET_CONFIG}(NET_CONFIG_ESP)
    net_transmit_void(O_BUFFER_CLEAR_RX_TX)

    // Wait 3 frames for the CLEAR_RX_TX command to work:
    {PPUSTATUS}()
    do while {PPUSTATUS}() < $80
    do while {PPUSTATUS}() < $80
    do while {PPUSTATUS}() < $80

    // Send a status request to make sure it's working:
    net_transmit_void(O_ESP_GET_STATUS)

    // Wait 1 frame for the ESP_GET_STATUS command to work:
    do while {PPUSTATUS}() < $80

    // Test if it worked:
    Bool result = {NET_RX}() >= $80 && net_in[1] == I_READY
    net_end_receive()
    return result

///////////////////////////////////////////////////////////////////////////////
// Rainbow Net Config /////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

ct U NET_CONFIG_ESP = %01
ct U NET_CONFIG_IRQ = %10

// Pass 'NET_CONFIG_' flags to enable them:
fn net_config_enable(U flags)
: +inline
    {NET_CONFIG}({NET_CONFIG}() | flags)

// Pass 'NET_CONFIG_' flags to disable them:
fn net_config_disable(U flags)
: +inline
    {NET_CONFIG}({NET_CONFIG}() & ~flags)

///////////////////////////////////////////////////////////////////////////////
// Transmitting ///////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// Initiates transmission and blocks until a message has been transmitted:
fn net_transmit()
: +inline
    U undefined
    {NET_TX}(undefined)
    do while {NET_TX}() < $80

// Transmits a single message pointed-to by 'msg':
fn net_transmit_data(CCC/net dat)
: -inline
    U length = dat[0]
    for U i = 0; i < length; i += 1
        net_out[i] = dat[i]
    net_transmit()

// Transmit a 1-byte length command message:
fn net_transmit_void(U command)
: +inline
    net_out[0] = 1
    net_out[1] = command
    net_transmit()

// Transmit a 1-byte length command message with a 1-byte argument:
fn net_transmit_u(U command, U value)
: +inline
    net_out[0] = 2
    net_out[1] = command
    net_out[2] = value
    net_transmit()

// Transmit a 1-byte length command message with arguments:
fn net_transmit_uu_string(U command, UU uu, CCC/ascii string)
    U strlen = ascii_strlen(string)
    net_out[0] = 4 + strlen
    net_out[1] = command
    net_out[2] = uu.b
    net_out[3] = uu.a
    net_out[4] = strlen
    for U i = 0; i < strlen; i += 1
        net_out[5 + i] = string[i]
    net_transmit()

///////////////////////////////////////////////////////////////////////////////
// Receiving //////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// Blocks until a message has been received:
// (Call 'net_end_receive' after processing the message!)
fn net_begin_receive()
: +inline
    do while {NET_RX}() < $80

// Returns true if a message has been received and is waiting:
// (Call 'net_end_receive' after processing the message!)
fn net_has_received() Bool
: +inline
    return {NET_RX}() >= $80

// Call once you're done with the received message:
fn net_end_receive()
: +inline
    U undefined
    {NET_RX}(undefined)

//
// The following functions use callbacks to handle their behavior.
// Receiver callbacks must belong to the 'net_receiver' fn set.
//

// Here's where we declare the global receiver handler.
// Simply assign this pointer to change the receiving behavior.
vars /net
    Fn.net_receiver net_receiver_ptr = @(net_receiver.ignore)

// This receiver handler does nothing:
fn net_receiver.ignore()
    return

// Blocks until a single message is received, using 'net_receiver_ptr' to process it:
fn net_receive()
: -inline
    net_begin_receive()
    net_receiver_ptr()
    net_end_receive()

// Receives messages that are already ready, using 'net_receiver_ptr' to process it.
// Returns the number of messages received (255 max).
fn net_receive_non_blocking() U
: -inline
    U count = 0
    while net_has_received()
        net_receiver_ptr()
        net_end_receive()
        count += 1
        if count == $FF
            break
    return count

// Receives messages until finding one that matches 'command_id',
// then returns without processing it.
// Non-matching messages are processed using 'net_receiver_ptr'.
fn net_receive_specific(U command_id)
: -inline
    while true
        net_begin_receive()
        if net_in[1] == command_id
            return
        net_receiver_ptr()
        net_end_receive()

// Receives messages until finding one that matches 'command_id',
// then returns a value of type U from that message.
// Non-matching messages are processed using 'net_receiver_ptr'.
fn net_receive_specific_u(U command_id) U
: +inline
    net_receive_specific(command_id)
    U result = net_in[2]
    net_end_receive()
    return result

// Receives messages until finding one that matches 'command_id',
// then returns a value of type UU from that message.
// Non-matching messages are processed using 'net_receiver_ptr'.
fn net_receive_specific_uu(U command_id) UU
: +inline
    net_receive_specific(command_id)
    UU result
    result.b = net_in[2]
    result.a = net_in[3]
    net_end_receive()
    return result

///////////////////////////////////////////////////////////////////////////////
// Helper functions ///////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

fn net_server_status() U
    net_transmit_void(O_SERVER_GET_STATUS)
    return net_receive_specific_u(I_SERVER_STATUS)

///////////////////////////////////////////////////////////////////////////////
// RNG functions //////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

//
// NOTE: The following functions use 'net_receiver_ptr' when handling 
//       other messages.
//

// Returns a random byte:
fn net_rand() U
    net_transmit_void(O_RND_GET_BYTE)
    return net_receive_specific_u(I_RND_BYTE)

// Returns a random value of type UU
fn net_rand_uu() UU
    net_transmit_void(O_RND_GET_WORD)
    return net_receive_specific_uu(I_RND_WORD)

// Returns a random byte between 'minv' and 'maxv':
fn net_randr(U minv, U maxv) U
    net_out[0] = 3
    net_out[1] = O_RND_GET_BYTE_RANGE
    net_out[2] = minv
    net_out[3] = maxv
    net_transmit()
    return net_receive_specific_u(I_RND_BYTE)
    
// Returns a random byte between 'minv' and 'maxv':
fn net_randr_uu(UU minv, UU maxv) UU
    net_out[0] = 5
    net_out[1] = O_RND_GET_WORD_RANGE
    net_out[2] = minv.b
    net_out[3] = minv.a
    net_out[4] = maxv.b
    net_out[5] = maxv.a
    net_transmit()
    return net_receive_specific_uu(I_RND_WORD)

// Returns a random byte less than 'bound':
// (Or 0 when 'bound' is 0)
fn net_randb(U bound) U
    if bound == 0
        return 0
    return net_randr(0, bound-1)

// Returns a random UU value less than 'bound':
// (Or 0 when 'bound' is 0)
fn net_randb_uu(UU bound) UU
    if bound == 0
        return 0
    return net_randr_uu(0, bound-1)
