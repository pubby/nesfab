/*
 * Copyright (c) 2022, Patrick Bene
 * This file is distributed under the Boost Software License, Version 1.0.
 * See LICENSE_1_0.txt or https://www.boost.org/LICENSE_1_0.txt 
 */

// RNG means random number generator.
// This file contains code for generating random numbers.

vars /rng
    U[2] rng_state = U[2](1, 1)

// Helper function for setting 'rng_state'.
fn seed(UU nonzero)
: +inline
    rng_state[0] = nonzero.a
    rng_state[1] = nonzero.b

// Returns a pseudo-random 8-bit value.
asm fn rand() U
: employs /rng
    default
        lda &rng_state+0
        asl
        rol &rng_state+1
        bcc done_xor_0
        eor #$2D // apply XOR feedback whenever a 1 bit is shifted out
    label done_xor_0
        asl
        rol &rng_state+1
        bcc done_xor_1
        eor #$2D
    label done_xor_1
        sta &rng_state+0
        sta &return
        rts

// Returns a pseudo-random 16-bit value.
fn rand16() UU
: +inline
    UU result
    result.a = rand()
    result.b = rand()
    return result

// Returns a bitmask that can hold any value less than or equal to argument 'num'.
asm fn ceil_mask(U num) U
: employs
    default
        ldx #$FF
        lda &num
    label loop
        inx
        lsr
        bne loop
        lda table, x
        sta &return
        rts
    label table
        U[9](%0, %1, %11, %111, %1111, %11111, %111111, %1111111, %11111111)

fn randb(U bound) U
    U mask = ceil_mask(bound)
    U result
    do while result > bound
        result = rand() & mask
    return result
